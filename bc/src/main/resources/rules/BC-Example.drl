package com.sample;

import com.sample.Location;

dialect  "mvel"

query isContainedIn( String x, String y )
  Location( x, y; ) //Positional arguments are ones where you don't need to specify the field name,
  // as the position maps to a known named field. i.e. Person( name == "mark" ) can be rewritten as Person( "mark"; ). 
  //The semicolon ';' is important so that the engine knows that everything before it is a positional argument.
  // Otherwise we might assume it was a boolean expression,
  // which is how it could be interpreted after the semicolon. 
  or
  ( Location( z, y; ) and isContainedIn( x, z; ) )
end

rule "go" salience 10
when
    $s : String(  )
then
    System.out.println( $s );
end


rule "go1"
when
    String( this == "go1" )
    isContainedIn("Office", "House"; )
then
    System.out.println( "office is in the house" );
end

rule "go2"
when
    String( this == "go2" )
    isContainedIn("Draw", "House"; )
then
    System.out.println( "Draw in the House" );
end

rule "go3"
when
    String( this == "go3" )
    isContainedIn("Key", "Office"; )
then
    System.out.println( "Key in the Office" );
end


rule "go4"
when
    String( this == "go4" )
    isContainedIn(thing, "Office"; )
then
    System.out.println( "thing " + thing + " is in the Office" );
end

rule "go5"
when
    String( this == "go5" )
    isContainedIn(thing, location; )
then
    System.out.println( "thing " + thing + " is in " + location );
end
